{
  parserClass='jp.s6n.idea.typespec.lang.parser.TypeSpecParser'
  parserUtilClass='jp.s6n.idea.typespec.lang.parser.TypeSpecParserUtil'

  implements='jp.s6n.idea.typespec.lang.psi.TypeSpecElement'
  extends='jp.s6n.idea.typespec.lang.psi.impl.TypeSpecElementImpl'

  elementTypeHolderClass='jp.s6n.idea.typespec.lang.psi.TypeSpecElementTypes'
  elementTypeClass='jp.s6n.idea.typespec.lang.psi.TypeSpecElementType'
  tokenTypeClass='jp.s6n.idea.typespec.lang.psi.TypeSpecTokenType'

  psiClassPrefix='TypeSpec'
  psiImplClassSuffix='Impl'
  psiPackage='jp.s6n.idea.typespec.lang.psi'
  psiImplPackage='jp.s6n.idea.typespec.lang.psi.impl'
  psiImplUtilClass='jp.s6n.idea.typespec.lang.psi.impl.TypeSpecImplUtil'

  tokens=[
    IMPORT          = 'import'
    USING           = 'using'
    NAMESPACE       = 'namespace'
    ENUM            = 'enum'
    UNION           = 'union'
    MODEL           = 'model'
    INTERFACE       = 'interface'
    EXTENDS         = 'extends'
    IS              = 'is'
    OP              = 'op'
    ALIAS           = 'alias'
    EXTERN          = 'extern'
    DEC             = 'dec'
    VALUEOF         = 'valueof'

    LBRACE          = '{'
    RBRACE          = '}'
    LPAREN          = '('
    RPAREN          = ')'
    LBRACKET        = '['
    RBRACKET        = ']'
    EQ              = '='
    DOTDOTDOT       = '...'
    DOT             = '.'
    COLON           = ':'
    SEMICOLON       = ';'
    QUEST           = '?'
    AT              = '@'
    COMMA           = ','
    PIPE            = '|'

    STRING_LITERAL  = 'regexp:"([^\\"\r\n]|\\[^\r\n])*"?'
    NUMERIC_LITERAL = 'regexp:-?\d+(\.\d+)?'
    IDENTIFIER      = 'regexp:[a-zA-Z][a-zA-Z0-9\-_]*|`(\\`|[^`])+`'
    WHITE_SPACE     = 'regexp:\s+'

    DOC_COMMENT     = 'regexp:/\*\*([^*]|\*[^/])*\*/'
    BLOCK_COMMENT   = 'regexp:/\*([^*]|\*[^/])*\*/'
    LINE_COMMENT    = 'regexp://.*'
  ]
}

File ::= Statement*

Statement ::=
    ImportStatement
    | UsingStatement
    | NamespaceStatement
    | EnumStatement
    | UnionStatement
    | ModelStatement
    | InterfaceStatement
    | OperationStatement
    | AliasStatement
    | ExternDecoratorStatement

ImportStatement ::= IMPORT STRING_LITERAL SEMICOLON {extends=Statement}

UsingStatement ::= USING Path SEMICOLON {extends=Statement}

NamespaceStatement ::= Decorator* NAMESPACE Path (SEMICOLON | LBRACE Statement* RBRACE) {extends=Statement}

EnumStatement ::= Decorator* ENUM Identifier EnumVariantsBlock {
    extends=Statement
    methods=[getPresentation]
}

EnumVariantsBlock ::= LBRACE [EnumVariant (COMMA EnumVariant)* COMMA?] RBRACE

EnumVariant ::= Decorator* Identifier

UnionStatement ::= Decorator* UNION Identifier UnionVariantsBlock {
    extends=Statement
    methods=[getPresentation]
}

UnionVariantsBlock ::= LBRACE [UnionVariant (COMMA UnionVariant)* COMMA?] RBRACE

UnionVariant ::= Decorator* Identifier COLON PathType

ModelStatement ::= Decorator* MODEL Identifier (ModelExtends | ModelIs)? (SEMICOLON | ModelPropertiesBlock) {
    extends=Statement
    methods=[getPresentation]
}

ModelExtends ::= EXTENDS PathType

ModelIs ::= IS PathType

ModelPropertiesBlock ::= LBRACE ModelProperty* RBRACE

ModelProperty ::= Decorator* Identifier QUEST? COLON Type SEMICOLON

InterfaceStatement ::= Decorator* INTERFACE Identifier InterfaceExtends? InterfaceOperationsBlock {
    extends=Statement
    methods=[getPresentation]
}

InterfaceExtends ::= EXTENDS PathType

InterfaceOperationsBlock ::= LBRACE InterfaceOperation* RBRACE

InterfaceOperation ::= Decorator* Operation SEMICOLON

OperationStatement ::= Decorator* OP Operation SEMICOLON {
    extends=Statement
    methods=[getPresentation]
}

AliasStatement ::= ALIAS Identifier EQ Type SEMICOLON {
    extends=Statement
    methods=[getPresentation]
}

ExternDecoratorStatement ::= EXTERN DEC Identifier ArgumentList SEMICOLON {
    extends=Statement
    methods=[getPresentation]
}

Operation ::= Identifier ArgumentList COLON Type

ArgumentList ::= LPAREN [Argument (COMMA Argument)*] RPAREN

Argument ::=
    NamedArgument
    | VariadicArgument

NamedArgument ::= Decorator* Identifier QUEST? COLON Type

VariadicArgument ::= DOTDOTDOT Identifier

Decorator ::= AT PathExpression ArgumentsList?

ArgumentsList ::= LPAREN [Expression (COMMA Expression)*] RPAREN

Expression ::=
    ObjectExpression
    | PathExpression
    | LiteralExpression

ObjectExpression ::= LBRACE [ObjectExpressionProperty (COMMA ObjectExpressionProperty)* COMMA?] RBRACE {extends=Expression}

ObjectExpressionProperty ::= Identifier COLON Expression

PathExpression ::= Path {extends=Expression}

LiteralExpression ::= STRING_LITERAL | NUMERIC_LITERAL {extends=Expression}

Type ::=
    UnionType
    | ValueOfType
    | ArrayType
    | PathType
    | LiteralType

ValueOfType ::= VALUEOF Type {extends=Type}

UnionType ::= Type (PIPE Type)+ {extends=Type}

ArrayType ::= Type LBRACKET RBRACKET {extends=Type}

PathType ::= Path {extends=Type}

LiteralType ::= STRING_LITERAL | NUMERIC_LITERAL {extends=Type}

Path ::= Identifier (DOT Identifier)*

private Identifier ::= IDENTIFIER
